plugins {
    id 'java-library'
    
    // https://docs.gradle.org/current/userguide/publishing_maven.html
    id 'maven-publish'
}

group = 'uk.co.magictractor'
version = '0.0.1-SNAPSHOT'

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        
            pom {
                name = "${project.name.capitalize()}"
                description = 'Create PDFs and other documents using a builder that abstracts use of Apache FOP.'
                url = "https://github.com/magictractor/${project.name}"
                inceptionYear = '2024'

                licenses {
                    license {
                        name = 'Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0'
                    }
                }
                developers {
                    developer {
                        id = 'kend'
                        name = 'Ken Dobson'
                       // email = 'me@gmail.com'
                    }
                }
                scm {
                    // connection = 'scm:git:git:github.com/magictractor/${project.name}.git'
                    // developerConnection = 'scm:git:ssh://github.com/magictractor/${project.name}.git'
                    url = "https://github.com/magictractor/${project.name}"
                }
            }
        }
    }

    repositories {
        //maven {
        //    name = 'myRepo'
        //    url = layout.buildDirectory.dir("repo")
        //}
    }
}

// https://docs.gradle.org/current/userguide/declaring_repositories.html
repositories {
    mavenCentral()
}

test {
    useJUnitPlatform()
}

java {
    // Restricted to Java 8 to match Apache FOP.
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
    
    withSourcesJar()
    //withJavadocJar()
}

tasks.withType(Jar) {
    destinationDirectory.set(file("$rootDir/jars"))
}

clean {
    delete "$rootDir/jars"
}

// "libs.xxx" refers to libraries configured in version catalog in settings.gradle.
dependencies {
    // Logger API.
    implementation libs.slf4j.api
    implementation libs.jcl.over.slf4j
    // Logger implementation for unit tests and main() methods.
    runtimeOnly libs.logback.classic

    implementation libs.guava
    
    implementation("org.apache.xmlgraphics:fop-core:2.10")  {
      // Excluded because classes conflict with newer JDKs.
      exclude group: "xml-apis", module: "xml-apis"
      // Excluded because jcl-over-slf4j is used.
      exclude group: "commons-logging", module: "commons-logging"
    }
    
    testImplementation libs.junit.jupiter;
    testRuntimeOnly libs.junit.jupiter.platform;
    testImplementation libs.assertj;
    // Mockito 4 for Java 8. See https://github.com/mockito/mockito/issues/2997.
    testImplementation("org.mockito:mockito-core:4.11.0")
}
